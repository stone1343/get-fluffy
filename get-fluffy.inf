!% -~S~DGd2s
!% +include_path=inform6lib

! During development
! -SDGd2s
! For release to public
! -~S~DGd2s

!  S   compile strict error-checking at run-time (on by default)
!  D   insert "Constant DEBUG;" automatically
!  G   compile a Glulx game file

!  d   contract double spaces after full stops in text
!  d2  contract double spaces after exclamation and question marks, too
!  s   give statistics

!----------------------------------------------------------------------------
! Banner defines Story, Headline, Release and Serial
!----------------------------------------------------------------------------
Constant Story "Get Fluffy!";
Constant Headline "^An interactive diary of what happened when some thugs kidnapped my dog^
 Copyright (c) 2020-2024 Jeff Stone^";
Release 1;
! Default for Serial is yymmdd of date of compilation
Serial "241201";
! IFID -- see http://babel.ifarchive.org/
! Generated by http://www.tads.org/ifidgen/ifidgen 2020-05-31
Array UUID_ARRAY string "UUID://121D8790-D7FC-4B87-A5DC-BF401125EF83//"; #Ifdef UUID_ARRAY; #Endif;

!----------------------------------------------------------------------------
! Constants, Replace directives
!----------------------------------------------------------------------------
! Not gonna apologize, to me this is the correct spelling ;-)
Constant DIALECT_US;
Constant MAX_SCORE = 12;       ! Seeing photo of Fluffy, dialing 911 or 867-5309 on the house phone, getting to the roof, finding each of 5 tokens, playing any token, saying 'xyzzy' or using hand sanitizer
Constant MAX_TIME = 60;        ! You have 60 minutes from when you see the text message
Constant MAX_PHONE_LENGTH = 7;
Constant MAX_PHOTOGRAPHS = 10;

Constant MODE_OFF = 0;
Constant MODE_HOME = 1;
Constant MODE_MESSAGES = 2;
Constant MODE_CONTACTS = 3;
Constant MODE_CAMERA = 4;
Constant MODE_PHOTOS = 5;

! How many moves until the smartphone turns itself off
Constant SMARTPHONE_TIMEOUT = 10;
! How many moves until a Disappearing_Object disappears
Constant DISAPPEARING_OBJECT_TIMEOUT = 5;

! Override default number of flags in newflags.h, should be a power of 2, they start at 0
Constant FLAGCOUNT = 32;
! Flags for steps in solving the crime
Constant PLAYER_HAS_SEEN_TEXT = 0; ! Player knows Fluffy's been taken ;-(
Constant PLAYER_HAD_FLASHBACK = 1;
Constant PLAYER_KNOWS_ABOUT_202 = 2; ! Igor has revealed Vlad and Sergei's room number
Constant PLAYER_HAS_SEEN_LAPTOP = 3;
Constant PLAYER_HAS_PHOTOGRAPHED_LAPTOP = 4;
Constant PLAYER_KNOWS_WHERE_FLUFFY_IS = 5;
Constant PLAYER_TOOK_TOPLESS_PHOTO = 6;
Constant BRITNEY_KNOWS_ABOUT_FLUFFY = 7;
Constant IGOR_KNOWS_ABOUT_FLUFFY = 8;
! Other flags
Constant SMARTPHONE_LED_BLINKING = 9; ! Doesn't affect game play, instead use PLAYER_HAS_SEEN_TEXT
Constant EXAMINE_SCREEN_NOT_SMARTPHONE = 10;
! Points, only score once, no matter how many times you do it
Constant PLAYER_SAW_PHOTO_OF_FLUFFY = 11;
Constant PLAYER_DIALED_911 = 12;
Constant PLAYER_DIALED_8675309 = 13;
Constant PLAYER_GOT_TO_ROOF = 14;
Constant PLAYER_FOUND_PLATINUM_TOKEN = 15;
Constant PLAYER_FOUND_GOLD_TOKEN = 16;
Constant PLAYER_FOUND_SILVER_TOKEN = 17;
Constant PLAYER_FOUND_COPPER_TOKEN = 18;
Constant PLAYER_FOUND_BRONZE_TOKEN = 19;
Constant PLAYER_PLAYED_TOKEN = 20;
Constant PLAYER_SAID_XYZZY = 21;
Constant PLAYER_USED_HAND_SANITIZER = 22;
#Ifdef DEBUG;
! Only used in DrawStatusLine
Constant LAST_FLAG_USED = 22;
#Endif;

Constant heart = $2764;
Constant love = $1F60D;
Constant star = $2B50;
Constant magic = $2728;
Constant no_entry = $26D4;
Constant smiley = $1F600;

! Cyrillic letters, note 'yo' is out of order, don't blame me
! Simplest way to avoid warnings is just comment out the unused ones

! Uppercase
Constant ucA = 1040;
!Constant ucBe = ucA+1;
Constant ucVe = ucA+2;
!Constant ucGe = ucA+3;
!Constant ucDe = ucA+4;
!Constant ucYe = ucA+5;
!Constant ucYo = 1025;
!Constant ucZhe = ucA+6;
!Constant ucZe = ucA+7;
!Constant ucI = ucA+8;
!Constant ucY = ucA+9;
!Constant ucKa = ucA+10;
!Constant ucEl = ucA+11;
!Constant ucEm = ucA+12;
!Constant ucEn = ucA+13;
!Constant ucO = ucA+14;
!Constant ucPe = ucA+15;
!Constant ucEr = ucA+16;
Constant ucEs = ucA+17;
!Constant ucTe = ucA+18;
!Constant ucU = ucA+19;
!Constant ucF = ucA+20;
!Constant ucKha = ucA+21;
!Constant ucTse = ucA+22;
!Constant ucChe = ucA+23;
!Constant ucSh = ucA+24;
!Constant ucShch = ucA+25;
!Constant ucHard = ucA+26;
!Constant ucBI = ucA+27;
!Constant ucSoft = ucA+28;
!Constant ucE = ucA+29;
!Constant ucYu = ucA+30;
!Constant ucYa = ucA+31;

! Lowercase
Constant lcA = 1072;
!Constant lcBe = lcA+1;
Constant lcVe = lcA+2;
Constant lcGe = lcA+3;
Constant lcDe = lcA+4;
Constant lcYe = lcA+5;
!Constant lcYo = 1105;
!Constant lcZhe = lcA+6;
!Constant lcZe = lcA+7;
!Constant lcI = lcA+8;
Constant lcY = lcA+9;
Constant lcKa = lcA+10;
Constant lcEl = lcA+11;
!Constant lcEm = lcA+12;
Constant lcEn = lcA+13;
Constant lcO = lcA+14;
Constant lcPe = lcA+15;
Constant lcEr = lcA+16;
!Constant lcEs = lcA+17;
!Constant lcTe = lcA+18;
!Constant lcU = lcA+19;
!Constant lcF = lcA+20;
!Constant lcKha = lcA+21;
!Constant lcTse = lcA+22;
!Constant lcChe = lcA+23;
!Constant lcSh = lcA+24;
Constant lcShch = lcA+25;
!Constant lcHard = lcA+26;
!Constant lcBI = lcA+27;
!Constant lcSoft = lcA+28;
!Constant lcE = lcA+29;
!Constant lcYu = lcA+30;
!Constant lcYa = lcA+31;

!Replace LanguageTimeOfDay;
Replace DrawStatusLine;
Replace ScoreSub;
Replace ThinkSub;

Include "parser";

!----------------------------------------------------------------------------
! If overriding library messages, LibraryMessages object goes between parser and verblib
!----------------------------------------------------------------------------

Object LibraryMessages
 with before [;
  Answer, Blow, Buy, Climb, Close, Consult, Dig, Drink, Eat, Fill, Jump, No, Pull, Rub, Search, Sleep, Swim, Swing, SwitchOn, SwitchOff, Taste, Tie, Touch, Wake, WakeOther, Wave, WaveHands, Yes:
   if (FlagOn(PLAYER_HAS_SEEN_TEXT)) print_ret (preformatted)"Focus on rescuing Fluffy!";
   print_ret (preformatted)"That's not something you need to do in this game.";
  Attack, Burn, Cut, ThrowAt:
   print_ret (preformatted)"You should know violence isn't the answer.";
  Miscellany:
   switch (lm_n) {
    ! I beg your pardon?
    10: "Say what, now?";
    ! 27: "I didn't understand that sentence.", 29: "I didn't understand that number.", 38: "That's not a verb I recognize."
    27, 29, 38:
     Nonsense();
     rtrue;
    ! I can't see any such thing.
    !30:
    ! if (((lm_o == igor) && (lobby hasnt visited)) || ((lm_o == vlad) && (FlagOff(PLAYER_HAD_FLASHBACK))) || ((lm_o == anonymous) && (FlagOff(PLAYER_HAS_SEEN_TEXT)))) "Who?";
    ! print "I can't see ";
    ! if (lm_o has animate) {
    !  if (lm_o has female) print "her.";
    !  else print "him.";
    ! }
    ! else "that.";
   }
  Order:
   switch (lm_o) {
    igor: print_ret (IgorsVoice)"I'm working, Miss.";
    britney: if (FlagOn(BRITNEY_KNOWS_ABOUT_FLUFFY)) print_ret (BritneysVoice)"Katey, we need to focus on getting Fluffy!";
   }
  Pray: "Nothing happens.";
  Restart:
   switch (lm_n) {
    1: "Start a new diary? (y/n)";
   }
  ! Christina Aguilera "Beautiful", Lady Gaga "Born This Way", Spice Girls "Never Give Up On The Good Times", Katy Perry "Hot N Cold"
  Sing: print_ret (string)random("~I am beautiful^No matter what they say^Words can't bring me down...~", "~I'm on the right track, baby, I was born this way-ay...~",
                                 "~Never give up on the good times^Livin' it up is a state of mind...~",
                                 "~'Cause you're hot then you're cold^You're yes then you're no^You're in then you're out^You're up then you're down...~");
 ];

Include "verblib";

! Other extensions
Include ">newflags.h";
Include ">smartcantgo.h";

!----------------------------------------------------------------------------
! Attributes, global variables
!----------------------------------------------------------------------------
Global dialed_number;

! date/time sample code from http://www.eblong.com/zarf/glulx/datetimetest.inf
Array time_array --> 4;
Array date_array --> 9;
! Uninitialized until you see the text message
Global end_time = nothing;

Global help_level;
Global nonsense_level;

! Who and where did you call? These will be nothing if no call active, they're used to put the NPC back where they were when you hang up
! Only one call at a time
Global call_person = nothing;
Global call_room = nothing;
! In Photos mode
Global current_photo;

!! wintype_TextBuffer
!Global gg_tbwin = 0;
!Constant GG_TBWIN_ROCK 210;
!! wintype_Graphics
!Global gg_grwin = 0;
!Constant GG_GRWIN_ROCK 212;

! Classes
Include ">fluffy-classes.inf";
! The rooms, plus the people and objects in them
Include ">fluffy-rooms.inf";
Include ">fluffy-people.inf";
Include ">fluffy-objects.inf";
! The rest of the routines...
Include ">fluffy-routines.inf";

Include "grammar";

!----------------------------------------------------------------------------
! Extend and Verb directives, and action routines for new verbs
!----------------------------------------------------------------------------
Verb 'come'
 * 'here' -> ComeHere
 * 'with' 'me' -> FollowMe;
Verb 'follow'
 * 'me' -> FollowMe;
Verb 'stay'
 * 'here' -> StayHere;
Verb 'knock'
 * 'on' door -> Knock
 *      door -> Knock;
Verb 'call' 'dial' 'phone'
 * PhoneNumber -> CallNumber;
Verb 'hang'
 * 'up'  -> HangUp;
Verb 'photograph'
 * noun -> Photograph;
Verb 'swipe'
 * 'left' -> SwipeLeft
 * 'right' -> SwipeRight;
Verb 'share'
 * 'to'/'with' scope=Anybody -> Share;
! Send is almost the same as Share, except you can't "send with"
Verb 'send'
 * 'to' scope=Anybody -> Share;
Verb 'flashback'
 * -> Flashback;
Verb 'help'
 * -> Help;
! So Britney can take, even when you're on the phone with her
Extend 'take'
 * scope=Anything -> Take;
Verb 'xyzzy'
 * -> Xyzzy;
