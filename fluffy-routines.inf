! Glk and customization routines

! For no status window, GG_STATUSWIN_ROCK should just rtrue;
[ InitGlkWindow winrock;
 switch (winrock) {
  GG_STATUSWIN_ROCK:
   #Ifdef DEBUG;
   gg_statuswin_size = 4;
   #Ifnot;
   gg_statuswin_size = 3;
   #Endif;
 }
 rfalse; ! leaving out this line will lead to a messy crash!
];

[ HandleGlkEvent ev context abortres newcmd cmdlen;
 ! From http://www.firthworks.com/roger/informfaq/tt.html
 switch (ev-->0) {
  evtype_Timer:
   glk_request_timer_events(0);
   if (context == 1) {
    glk_cancel_char_event(gg_mainwin);
    abortres-->0 = 0;
   } else {
    ! http://adamcadre.ac/gull/gull-2n.html
    glk_cancel_line_event(gg_mainwin, 0);
    newcmd = "flashback";
    cmdlen = PrintAnyToArray(abortres+WORDSIZE, INPUT_BUFFER_LEN-WORDSIZE, newcmd);
    abortres-->0 = cmdlen;
   }
   return 2;
 }
];

[ Pause;
 glk_request_char_event(gg_mainwin);
 while(1) {
  glk_select(gg_event);
  if (gg_event-->0 == evtype_MouseInput or evtype_CharInput) {
   if (gg_event-->0 == evtype_MouseInput) glk_cancel_char_event(gg_mainwin);
   return;
  }
  else HandleGlkEvent(gg_event, 1, gg_arguments);
 }
];

[ CurrentTime;
 glk_current_time(time_array);
 glk_time_to_date_local(time_array, date_array);
 return date_array;
];

![ LanguageTimeOfDay hh mm;
! print hh/10, hh%10, ":", mm/10, mm%10;
!];

!----------------------------------------------------------------------------
! Initialise
!----------------------------------------------------------------------------
[ Initialise;
 if (~~(glk_gestalt(gestalt_DateTime, 0)) || ~~(glk_gestalt(gestalt_Timer, 0))) {
  print "Your browser does not support required time features, try a more recent browser. Thanks for trying to save Fluffy!";
  quit;
 }

 notify_mode = false;
 player = kaitlyn;
 location = street;

! if (gg_grwin == 0) {
!  ! For winmethod, use one of:
!  !  - winmethod_Above
!  !  - winmethod_Below
!  !  - winmethod_Left
!  !  - winmethod_Right
!  ! plus one of:
!  !  - winmethod_Fixed
!  !  - winmethod_Proportional
!  ! wintype:
!  !  - wintype_TextBuffer
!  !  - wintype_TextGrid
!  !  - wintype_Graphics
!  gg_grwin = glk_window_open(gg_mainwin, ((winmethod_Right+winmethod_Fixed)), 152, wintype_Graphics, GG_GRWIN_ROCK);
! }
! ! Image is defined in image_info_map in get-fluffy.html
! glk_image_draw(gg_grwin, 1, 0, 0);
! Pause();
! if (gg_grwin ~= 0) {
!  glk_window_close(gg_grwin, 0);
!  gg_grwin = 0;
! }

 SetFlag(SMARTPHONE_LED_BLINKING);
 Photograph.create(fluffy);
 ClearScreen();
 print "^I'll never forget those words...";
 ! Wait for user to press a key
 Pause();
 ClearScreen();
 print "^~We have your dog.~";
 ! Wait for user to press a key
 Pause();
 ClearScreen();
 print "^Omigod, someone took my Fluffy! I gotta get him back!!^";
 !#Ifdef DEBUG;
 !Pause();
 !ClearScreen();
 !glk_set_style(style_Preformatted);
 !print (char)ucA, (char)ucBe, (char)ucVe, (char)ucGe, (char)ucDe, (char)ucYe, (char)ucYo, (char)ucZhe, (char)ucZe, (char)ucI, (char)ucY, (char)ucKa, (char)ucEl, (char)ucEm, (char)ucEn, (char)ucO, (char)ucPe, (char)ucEr, (char)ucEs, (char)ucTe, (char)ucU, (char)ucF, (char)ucKha, (char)ucTse, (char)ucChe, (char)ucSh, (char)ucShch, (char)ucHard, (char)ucBI, (char)ucSoft, (char)ucE, (char)ucYu, (char)ucYa, "^";
 !print (char)lcA, (char)lcBe, (char)lcVe, (char)lcGe, (char)lcDe, (char)lcYe, (char)lcYo, (char)lcZhe, (char)lcZe, (char)lcI, (char)lcY, (char)lcKa, (char)lcEl, (char)lcEm, (char)lcEn, (char)lcO, (char)lcPe, (char)lcEr, (char)lcEs, (char)lcTe, (char)lcU, (char)lcF, (char)lcKha, (char)lcTse, (char)lcChe, (char)lcSh, (char)lcShch, (char)lcHard, (char)lcBI, (char)lcSoft, (char)lcE, (char)lcYu, (char)lcYa, "^";
 !glk_set_style(style_Normal);
 !#Endif;
 Pause();
 ClearScreen();

 print "^This is the story of the scariest thing that ever happened to me, it all started
        with a text message, someone had taken my cute little dog, Fluffy!
        But I'm getting ahead of myself, let me start at the beginning...^";
 banner();
 print "^My name's Kaitlyn, I've come to Las Vegas with my best friend, Britney ", (char)love, ", to show my Fluffy ",;
 glk_set_style(style_Alert);
 print (char)heart;
 glk_set_style(style_Normal);
 print (char)star, " at the Silver State Kennel Club Dog Show.
       We arrived an hour ago and I left Fluffy in my hotel room, 402, while I ran out for something to eat. Britney stayed in her room, right next to mine, to get settled in.^^";
 return 2;
 ! Story continues with street.initial()
];

! gg_statuswin is wintype_TextGrid, you can address individual (row, column)
! gg_statuswin_size is set in InitGlkWindow above
[ DrawStatusLine width height i o;
 ! If we have no status window, we must not try to redraw it.
 if (gg_statuswin == 0) return;
 ! If there's no player location, we shouldn't try either.
 if (location == nothing || parent(player) == nothing) return;
 glk_set_window(gg_statuswin);
 glk_window_clear(gg_statuswin);
 glk_window_get_size(gg_statuswin, gg_arguments, gg_arguments+4);
 width = gg_arguments-->0;
 height = gg_arguments-->1;
 ! Start drawing statusline, we know its dimensions
 if (location ~= thedark) {
  ! (row, column) coordinates are weird because the East and West dashes need to be doubled up
  if (location.nw_to) { AtXY(2, 0); print "@@92"; }
  if (location.n_to)  { AtXY(3, 0); print "|"; }
  if (location.ne_to) { AtXY(4, 0); print "/"; }

  if (location.w_to)  { AtXY(1, 1); print "--"; }
                        AtXY(3, 1); print "O";
  if (location.e_to)  { AtXY(4, 1); print "--"; }

  if (location.sw_to) { AtXY(2, 2); print "/"; }
  if (location.s_to)  { AtXY(3, 2); print "|"; }
  if (location.se_to) { AtXY(4, 2); print "@@92"; }
 }
 AtXY(8, 0);
 ! Labels start at column 8, length 14, so values will be printed at 22
 print "Location:     ", (name)location;
 AtXY(8, 1);
 ! Moves (sline2) not displayed
 print "Score:        ", sline1;
 if (end_time ~= nothing) {
  AtXY(8, 2);
  print "Minutes left: ", (end_time - glk_current_simple_time(60));
 }
 #Ifdef DEBUG;
  ! Leave 20 spaces, for "Elevator Vestibule  "
 AtXY(42, 0);
 for (i=0: i <= LAST_FLAG_USED: i++) {
  if (FlagOn(i)) print i, " ";
 }
 AtXY(42, 1);
 print "kb ", (name)parent(k_boobs), " b ", (name)parent(britney), " bb ", (name)parent(b_boobs), " bar ", (name)britney.ActualRoom();
 if (britney.destination_room ~= nothing) print " bdr ", (name)britney.destination_room;
 print " i ", (name)parent(igor);
 AtXY(42, 2);
 objectloop (o ofclass Disappearing_Object) {
  if (o.time_left > 0) print (name)o, " ", o.time_left, " ";
 }
 AtXY(42, 3);
 print "spm ", smartphone.mode, " spmc ", smartphone.move_counter;
 if (call_person ~= nothing && call_room ~= nothing) {
  if (telephone has general) print " House phone";
  else print " Smartphone";
  print " p ", (name)call_person, " r ", (name)call_room;
 }
 #Endif;
 glk_set_window(gg_mainwin);
];

! Just a shortcut for moving cursor in status line
[ AtXY x y;
 glk_window_move_cursor(gg_statuswin, x, y);
];

! Pronoun routines, from http://inform-fiction.org/manual/Answers.pdf
[ PronounAcc i;
 if (i hasnt animate || i has neuter) print "it";
 else { if (i has female) print "her"; else print "him"; }
];
! Not used yet, so commented out
![ PronounNom i;
! if (i hasnt animate || i has neuter) print "it";
! else { if (i has female) print "she"; else print "he"; }
!];
![ CPronounNom i;
! if (i hasnt animate || i has neuter) print "It";
! else { if (i has female) print "She"; else print "He"; }
!];

! Scope routines used for parsing grammar

[ Anything o;
 if (scope_stage == 1) rfalse;
 if (scope_stage == 2) {
  objectloop (o ofclass Object) PlaceInScope(o); rtrue;
 }
 if (nonsense_level > 0) print "Now ";
 "I'm talking about things that don't exist.";
];

[ Anybody o;
 if (scope_stage == 1) rfalse;
 if (scope_stage == 2) {
  objectloop (o ofclass Person) PlaceInScope(o); rtrue;
 }
 if (nonsense_level > 0) print "Part of my nonsense game is";
 else print "I'm";
 " making up names of people that don't exist.";
];

! Printing styles (the style names do not seem to be case-sensitive and seem to be used inconsistently, but they're used here the way they're defined in get-fluffy.css
! There's a few more which could be used if needed: Style_header, Style_subheader, Style_input, Style_alert

! AMS's font
[ BritneysVoice text;
 glk_set_style(Style_blockquote);
 print (string)text;
 glk_set_style(Style_normal);
];

! ID's font
[ IgorsVoice text;
 glk_set_style(Style_note);
 print (string)text;
 glk_set_style(Style_normal);
];

! Jenny, from the song 867-5309, NATS's font
[ JennysVoice text;
 glk_set_style(Style_user2);
 print (string)text;
 glk_set_style(Style_normal);
];

! Monospaced
[ preformatted text;
 glk_set_style(Style_preformatted);
 print (string)text;
 glk_set_style(Style_normal);
];

! Italic
[ i text;
 glk_set_style(Style_emphasized);
 print (string)text;
 glk_set_style(Style_normal);
];

! For things that score a point or are otherwise awesome
[ blue text;
 glk_set_style(Style_user1);
 print (string)text;
 glk_set_style(Style_normal);
];

! Utility routines

[ ActorHasKeyForDoor a d o;
 if (master_key in a) rtrue;;
 objectloop (o ofclass Room_Key) {
  if (o in a) {
   if (o.key_to_room == d.to_room) rtrue;
  }
 }
 rfalse;
];

[ NewRoom o;
 move k_boobs to location;
 if ((britney.is_following) || (call_person == britney)) {
  move britney to location;
  move b_boobs to location;
 }
 if (((location == elevator_vestibule) && (elevator.floor_number == 1)) || ((location == second_floor) && (elevator.floor_number == 2)) || ((location == third_floor) && (elevator.floor_number == 3)) || ((location == fourth_floor) && (elevator.floor_number == 4)) || ((location == fifth_floor) && (elevator.floor_number == 5))) give elevator_doors open;
 else if (((location == elevator_vestibule) && (elevator.floor_number ~= 1)) || ((location == second_floor) && (elevator.floor_number ~= 2)) || ((location == third_floor) && (elevator.floor_number ~= 3)) || ((location == fourth_floor) && (elevator.floor_number ~= 4)) || ((location == fifth_floor) && (elevator.floor_number ~= 5))) give elevator_doors ~open;
 ! When you move to a new room, timer starts for all Disappearing_Objects with 'general', i.e. have been dropped in a Public_Room
 objectloop (o ofclass Disappearing_Object) {
  if (o has general) {
   if ((parent(o) ofclass Public_Room) || (o in slot_machine)) {
    StartTimer(o, DISAPPEARING_OBJECT_TIMEOUT);
    give o ~general;
   }
  }
 }
];

[ DescribeDay hh;
 CurrentTime();
 hh = date_array-->4;
 print "It's a ";
 if ((hh >= 6) && (hh < 12)) print "beautiful morning";
 else if ((hh >= 12) && (hh < 18)) print "hot afternoon";
 else if ((hh >= 18) && (hh < 21)) print "warm evening";
 else print "cool night";
];

[ DisrobeTop actor noun;
 if (actor.wearing_top ~= noun) print_ret (string)actor.is, " not wearing the ", (name)actor.wearing_top, ".";
 if (actor == britney && FlagOff(BRITNEY_KNOWS_ABOUT_FLUFFY)) print_ret (BritneysVoice)"Why are you acting creepy, Katey?";
 actor.wearing_top = nothing;
 give noun ~worn;
 move actor.bra to actor;
 give actor.bra worn;
 if (actor == player) "I take off the ", (name)noun, ".";
 else print_ret (BritneysVoice)"Sure, but only to help you get Fluffy back!";
];

[ WearTop actor noun;
 if (actor.wearing_top ~= nothing) print_ret (string)actor.is, " already wearing the ", (name)actor.wearing_top, ".";
 if (noun notin actor) print_ret (string)actor.is, " not carrying the ", (name)noun, ".";
 remove actor.bra;
 give actor.bra ~worn;
 actor.wearing_top = noun;
 give noun worn;
 if (actor == player) "I put on the ", (name)noun, ".";
 else {
  print_ret (BritneysVoice)"Thanks, I feel a little less naked now!";
 }
];

! Originally from DM4, modified to return an integer
[ PhoneNumber at length dialed dialed_already i;
 dialed_number = 0;
 do {
  if (wn > num_words) jump number_ended;
  at = WordAddress(wn); length = WordLength(wn);
  for (i=0: i<length: i++) {
   switch (at->i) {
    '0', '1', '2', '3', '4', '5', '6', '7', '8', '9':
     if (dialed++ < MAX_PHONE_LENGTH) dialed_number = dialed_number*10 + (at->i - '0');
    '-': ;
    default: jump number_ended;
   }
  }
  wn++;
  dialed_already = dialed;
 } until (false);
 .number_ended;
 if (dialed_already == 0) return GPR_FAIL;
 return GPR_PREPOSITION;
];

! o is optional object to to determine if you're on the phone with that person
[ OnPhone o;
 if (o ~= nothing) return ((call_room ~= nothing) && (call_person == o));
 else return ((call_room ~= nothing) && (call_person ~= nothing));
];

[ CallNumberSub hundreds tens ones o;
 if (OnPhone()) "I'm currently on the phone with ", (name)call_person, ".";
 ! If player isn't holding the telephone, terminate
 if (telephone notin player) "With what?";
 ! 0 = Lobby
 if (dialed_number == 0) {
  ! If that's where you are, terminate
  if (player in lobby) "Doh! Busy signal.";
  give telephone general;
  call_person = igor;
  call_room = lobby;
  give igor concealed;
  move igor to location;
  print_ret (IgorsVoice)"Hello, Front Desk?";
 }
 ! 411 and 911
 if (dialed_number == 411 or 911) {
  ScoreFlag(PLAYER_DIALED_911);
  print_ret (preformatted)"External calls are not allowed.";
 }
 ! 867-5309
 if (dialed_number == 8675309) {
  ScoreFlag(PLAYER_DIALED_8675309);
  print_ret (JennysVoice)"Hi, this is Jenny, you know what to do!";
 }
 ! Three-digits
 if ((dialed_number > 0) && (dialed_number <= 999)) {
  hundreds = dialed_number/100;
  tens = (dialed_number%100)/10;
  ones = dialed_number%10;
  if ((hundreds == 2 or 3 or 4 or 5) && (tens == 0) && (ones == 1 or 2 or 3 or 4 or 5 or 6 or 7)) {
   ! It's a valid room number, find the room
   objectloop (o ofclass Hotel_Room) {
    if (o.room_number == dialed_number) {
     ! If you dialed the room you're in, terminate
     if (player in o) "Doh! Busy signal.";
     ! Found the room, is Britney in it?
     if (britney in o) {
      give telephone general;
      call_room = o;
      call_person = britney;
      give britney concealed;
      move britney to location;
      move b_boobs to location;
      print_ret (BritneysVoice)"Hello?";
     }
     break;
    }
   }
   "The phone rings and rings until I hang up.";
  }
 }
 print_ret (preformatted)"Please hang up and try your call again.";
];

! Doesn't print anything, caller is responible for that
[ EndCall o;
 give o ~concealed;
 move o to call_room;
 if (o == britney) move b_boobs to call_room;
 call_person = nothing;
 call_room = nothing;
 ! If hanging up house phone
 if (telephone has general) give telephone ~general;
 ! Else hanging up smartphone
 else {
  move contact_britney to screen;
  smartphone.interact();
 }
];

[ HangUpSub;
 if ((~~OnPhone())) "No call active at the moment.";
 if ((telephone hasnt general) && (smartphone.mode ~= MODE_CONTACTS)) "How?";
 print "~Bye, ", (name)call_person, ".~^";
 EndCall(call_person);
 rtrue;
];

[ ComeHereSub;
 ! The actual code is in Igor or Britney
 if (actor == player) rfalse;
];

[ FollowMeSub;
 ! The actual code is in Igor or Britney
 if (actor == player) rfalse;
];

[ StayHereSub;
 ! The actual code is in Igor or Britney
 if (actor == player) rfalse;
];

[ KnockSub;
 if (~~(noun ofclass Basic_Door)) "That doesn't make sense.";
];

[ PhotographSub o;
 if (OnPhone(noun)) "How should I photograph ", (name)noun, "when I'm on the phone with ", (PronounAcc)noun, "?";
 if ((smartphone notin player) || (smartphone.mode ~= MODE_CAMERA)) "With what?";
 if (noun ~= kaitlyn or britney or igor or laptop) "I decide not to waste photos on that.";
 smartphone.interact();
 o = Photograph.create(noun);
 if (o == nothing) "Crap, SD card is full.";
 switch (noun) {
  kaitlyn:
   if (o.wearing_top == nothing) "It's not like I don't already have a ton of topless photos of myself!";
   "I take a selfie.";
  britney, igor:
   print "I take a picture of ", (name)noun,".^";
   if ((noun == britney) && (noun.wearing_top == nothing)) print_ret (BritneysVoice)"^You can send that to those Russian scumbags if you want, I'm not shy!";
   else rtrue;
  laptop: "I take a picture of the message on the laptop.";
 }
];

! Previous
[ SwipeRightSub;
 if ((smartphone notin player) || (smartphone.mode ~= MODE_PHOTOS)) "With what?";
 smartphone.interact();
 if (current_photo > 1) current_photo--;
 else "Nothing happens.";
];

! Next
[ SwipeLeftSub;
 if ((smartphone notin player) || (smartphone.mode ~= MODE_PHOTOS)) "With what?";
 smartphone.interact();
 if (current_photo < (MAX_PHOTOGRAPHS - Photograph.remaining())) current_photo++;
 else "Nothing happens.";
];

! Can only send to Britney or Anonymous, photos can only be of Fluffy, Igor, Kaitlyn, Britney or the laptop
[ ShareSub o;
 if ((smartphone notin player) || (smartphone.mode ~= MODE_PHOTOS)) "Share what?";
 if (noun == igor or vlad) "I don't know ", (name)noun, "'s cell number.";
 if (noun == player) "I can't send to myself.";
 smartphone.interact();
 ! Find current photo
 objectloop (o ofclass Photograph) {
  if (o.number == current_photo) {
   if (noun == britney) {
    switch (o.subject) {
     fluffy: "I've probably sent her a bajillion photos of Fluffy!";
     igor: "She probably doesn't care about a photo of Igor, but why not?";
     kaitlyn, britney:
      print "She'll like this ";
      if (o.wearing_top == nothing) print "topless ";
      print "photo of ";
      if (o.subject == kaitlyn) "me.";
      "herself.";
     laptop:
      print "Maybe she can help me figure out ";
      if (FlagOff(PLAYER_KNOWS_WHERE_FLUFFY_IS)) "what this means!";
      "how to get to the basement!";
    }
   }
   else {
    switch (o.subject) {
     fluffy, igor: "This is *NOT* the photo they wanted, maybe it'll piss them off!";
     kaitlyn, britney:
      if (o.wearing_top == nothing) {
       if (o.subject == britney) deadflag = 4;
       else deadflag = 5;
      }
      "No, I'm not gonna give them anything unless it helps me!";
     laptop: "I decide not to reveal what I know.";
    }
   }
   break;
  }
 }
];

[ TriggerFlashback;
 glk_request_timer_events(5000);
];

[ FlashbackSub;
 SetFlag(PLAYER_HAD_FLASHBACK);
 "Hey, I just remembered, when I was going up to my room earlier, there were two guys checking me out.
  It was really creepy. They were speaking some foreign language and laughing at Fluffy and me.
  I think I heard one guy call the other one Vlad. Maybe Igor can tell me something about them.";
];

! Hints, which will direct you to the most desirable outcome, arresting the Russian scumbags
[ ThinkSub;
 if (FlagOff(PLAYER_HAS_SEEN_TEXT)) "I know, I should check my messages.";
 if (FlagOff(PLAYER_HAD_FLASHBACK)) "Maybe I should explore the hotel a little more.";
 if (FlagOff(IGOR_KNOWS_ABOUT_FLUFFY)) "Would Igor help if he knew Fluffy's been dognapped?";
 if (FlagOff(PLAYER_KNOWS_ABOUT_202)) "Maybe Igor could tell me something about this Vlad guy.";
 if (FlagOff(PLAYER_HAS_SEEN_LAPTOP)) {
  if ((parent(master_key) == nothing or roof) || parent(master_key) ofclass Hotel_Room) ConsiderSendingPhoto();
  if (master_key notin player) "I need a key to get into the Russians' room.";
  if (location ~= room_202) "I'm sure I could find some evidence in the Russians' room.";
  "I wish I was more observant!";
 }
 if (FlagOff(PLAYER_HAS_PHOTOGRAPHED_LAPTOP)) "I should record what the message says.";
 if (FlagOff(PLAYER_KNOWS_WHERE_FLUFFY_IS)) "Igor's Ukrainian, maybe he can translate the message.";
 ! At this point, you know where Fluffy is
 if ((parent(master_key) == nothing or roof) || parent(master_key) ofclass Hotel_Room) ConsiderSendingPhoto();
 if (master_key notin player) "I need the master key to get into the basement.";
 "Time to go rescue Fluffy!";
];

[ ConsiderSendingPhoto;
 if (FlagOff(PLAYER_TOOK_TOPLESS_PHOTO)) "Since I've lost the master key, I might have to actually take a topless photo.";
 "I might have to send the photo to those scumbags.";
];

! Everything this prints is in the computer's voice
[ HelpSub;
 glk_set_style(style_Preformatted);
 switch (help_level++) {
  0: print "You can walk around by simply saying ~north~, ~south~ or even just ~e~, ~w~, etc.
            Try interacting with the people in the game with ~ask~, ~tell~, ~show~ or ~give~.
            If you find objects, you can ~examine~ or ~take~ them and you can check what you're carrying with ~inventory~.";
  1: print "Your smartphone is very powerful, try using it.";
  default:
   print "If you think carefully, additional insight might come to you.";
   if (((parent(master_key) == nothing) || (parent(smartphone) == nothing)) && FlagOff(PLAYER_SAID_XYZZY)) print " Also, magic words have been known to retrieve critical objects that have been lost.";
 }
 glk_set_style(style_Normal);
 new_line;
 rtrue;
];

! Reference to 'xyzzy' in The Colossal Cave, if you've lost the master key and/or your smartphone, you can get them back!
[ XyzzySub itDidSomething;
 if (FlagOff(PLAYER_SAID_XYZZY)) {
  ScoreFlag(PLAYER_SAID_XYZZY);
  itDidSomething = false;
  if (parent(master_key) == nothing) {
   move master_key to location;
   print (char)magic, " Like magic, the master key";
   itDidSomething = true;
  }
  if (parent(smartphone) == nothing) {
   move smartphone to location;
   if (~~itDidSomething) print (char)magic, " Out of nowhere,";
   else print " and";
   print " my smartphone";
   itDidSomething = true;
  }
  if (itDidSomething) print_ret " appeared!!! ", (char)magic;
 }
 ! "Nothing happens"
 <<Pray>>;
];

! This eventually becomes 'meta', Kaitlyn realizes she's being manipulated
[ Nonsense;
! if (gg_tbwin == 0) {
!  gg_tbwin = glk_window_open(gg_mainwin, ((winmethod_Below+winmethod_Fixed)), 5, wintype_TextBuffer, GG_TBWIN_ROCK);
! }
! glk_set_window(gg_tbwin);
! glk_window_clear(gg_tbwin);
 switch (nonsense_level++) {
  0: print "Sometimes I say things that make no sense, just for fun...";
  1, 2, 3: print (string)random("More nonsense...", "Again with the gibberish...", "There I go again...");
  ! Synonyms for 'nonsense', mostly from http://phrontistery.info/nonsense.html
  4, 5, 6, 7, 8, 9, 10, 11, 12: print (string)random("Hooey!", "Malarkey!", "Poppycock!", "Mumbo-jumbo!", "Jabberwocky!", "Baloney!", "Balderdash!", "Flim-flam!", "Hogwash!");
  ! From https://examples.yourdictionary.com/examples-of-gibberish.html
  13, 14, 15, 16: print (string)random("Supercalifragilisticexpialidocious!", "Gollygoops!", "Piggly-wiggly!", "Woopsiedoo!");
  17: print "I keep saying things that make no sense!";
  18: print "Why can't I stop saying silly things?";
  19: print "I'm starting to question my sanity!";
  20: print "Or maybe I'm not in control of my own actions?";
  21: print "Could there be something out there making me say these things?";
  22: print "Maybe 'free will' really is an illusion!";
  23: print "Am I losing my mind?";
  24: print "I'm starting to be convinced there's something, or someone, out there pulling the strings.";
  25: print "Whoever it is out there, I hope they're enjoying making me squirm!";
  26: print "I give up, I'm just a pawn of some 'superior' being.";
  27: print "Hey, you, out there, enough!!";
  28: print "Screw you, 'god' or whatever you call yourself!";
  29: print "I'm not listening to you anymore.";
  default: print "La-la-la-la, not listening!";
 }
! glk_set_window(gg_mainwin);
! Pause();
! if (gg_tbwin ~= 0) {
!  glk_window_close(gg_tbwin, 0);
!  gg_tbwin = 0;
! }
 new_line;
 rtrue;
];

[ DeathMessage;
 glk_set_window(gg_statuswin);
 ! Blank out the compass rose
 AtXY(2, 0); spaces 3;
 AtXY(1, 1); spaces 5; ! The middle row is 5 wide
 AtXY(2, 2); spaces 3;
 ! Blank out Location, longest is 'Elevator Vestibule" (18)
 AtXY(22, 0);
 spaces 18;
 ! Blank out Minutes Left
 AtXY(22, 2);
 spaces 2;
 glk_set_window(gg_mainwin);
 switch (deadflag) {
  3:
   print "After a bit of exploring in the basement, I found Fluffy ";
   glk_set_style(style_Alert);
   print (char)heart,(char)heart,(char)heart;
   glk_set_style(style_Normal);
   print ", he was s-o-o-o happy to see me!!!";
   if ((laptop in player) || FlagOn(PLAYER_HAS_PHOTOGRAPHED_LAPTOP)) print " Plus, we had all the evidence we needed to put the Russians in jail for a very long time.";
   print " Fluffy came second in his class in the dog show, but after what happened that weekend, I didn't care!";
  4:
   print "Britney didn't mind me sending the topless photo of her to the Russians, but it still wasn't something I was happy about doing.
          Anyway, we got Fluffy back and neither of us will ever forget that weekend!";
  5:
   print "After sending my topless photo to the Russians, they returned Fluffy and immediately posted the photo online.
          I always wanted to be famous, so this wasn't such a bad ending!";
  6:
   print "I ran out of time, ";
   if (PLAYER_KNOWS_WHERE_FLUFFY_IS) print "but fortunately hotel staff found Fluffy in the basement and returned him to me!";
   else {
    print "apparently the scumbag";
    if (FlagOn(PLAYER_HAD_FLASHBACK)) print " Russians had Fluffy";
    else print "s that took Fluffy had him";
    print " somewhere in the basement, so we got him back safely, which was all that really mattered!";
   }
   print " Since we never caught them, ";
   if (FlagOn(PLAYER_HAD_FLASHBACK)) print "the Russians are still out there, telling Donald Trump what to do.";
   print "the bad guys are still out there.";
 }
];

[ ScoreFlag flag;
 if (FlagOff(flag)) {
  SetFlag(flag);
  score++;
 }
];

[ ScoreSub;
 "You got ", score, " of ", MAX_SCORE, " bonus points!";
];
